Option Explicit

'============================
' Settings
'============================
Private Const LINK_RATIO As Double = 0.17

Private Const SH_PORT As String = "Sheet2"
Private Const SH_CHG  As String = "Sheet3"
Private Const SH_OUT  As String = "Sheet4"

Private Const BBG_SUFFIX As String = " JT Equity"

'============================
' Sheet4 column mapping
'============================
Private Const C_CODE     As Long = 1    'A LOCAL_CODE
Private Const C_TICKER   As Long = 2    'B Ticker
Private Const C_NAME     As Long = 3    'C Name
Private Const C_PX       As Long = 4    'D JPY Price
Private Const C_VOL20    As Long = 5    'E VOLUME_AVG_20D
Private Const C_PRE_TOT  As Long = 6    'F PRE_TOT_SHARES
Private Const C_POST_TOT As Long = 7    'G POST_TOT_SHARES
Private Const C_PRE_FFW  As Long = 8    'H PRE_FFW
Private Const C_POST_FFW As Long = 9    'I POST_FFW
Private Const C_PRE_IDX  As Long = 10   'J PRE_IDX_SHARES
Private Const C_POST_IDX As Long = 11   'K POST_IDX_SHARES
Private Const C_CAT      As Long = 12   'L Category
Private Const C_EVENTS   As Long = 13   'M Events (Description)
Private Const C_PRE_W    As Long = 14   'N Curr Idx Wgt %
Private Const C_POST_W   As Long = 15   'O New Idx Wgt %
Private Const C_DW       As Long = 16   'P Wgt Change
Private Const C_TRD_JPY  As Long = 17   'Q Value to Trade (JPY)"
Private Const C_TRD_USD  As Long = 18   'R Value to Trade (USD)
Private Const C_SH_TRADE As Long = 19   'S Shares to Trade
Private Const C_LIQ      As Long = 20   'T Liquidity

'============================
' STEP1
'============================
Public Sub STEP1_Prepare_Sheet4_BBG()
    Dim rebDate As Long
    rebDate = CLng(InputBox("Rebalance Date (yyyymmdd)", "Rebalance", "20260130"))
    If rebDate = 0 Then Exit Sub

    Dim wsP As Worksheet, wsC As Worksheet, wsO As Worksheet
    Set wsP = ThisWorkbook.Worksheets(SH_PORT)
    Set wsC = ThisWorkbook.Worksheets(SH_CHG)
    Set wsO = ThisWorkbook.Worksheets(SH_OUT)

    Application.ScreenUpdating = False
    Application.Calculation = xlCalculationManual

    Dim baseDate As Long
    baseDate = GetBaseDate(wsP)

    Dim baseDict As Object, preDict As Object, postDict As Object
    Set baseDict = LoadPortfolio_S2(wsP)

    Set preDict = CloneHoldings(baseDict)
    ApplyChanges_S3_Overwrite preDict, wsC, baseDate + 1, rebDate - 1

    Set postDict = CloneHoldings(preDict)
    ApplyChanges_S3_Overwrite postDict, wsC, rebDate, rebDate

    PrepareSheet4Layout wsO
    FillSheet4UniverseAndLevels wsO, preDict, postDict
    SetBBGFormulas wsO

    ' V列（Liquidityの右隣）にパラメータラベル・値欄
    With wsO
        .Cells(1, C_LIQ + 1).Value = "Parameters"
        .Cells(2, C_LIQ + 1).Value = "USDJPY"
        .Cells(2, C_LIQ + 2).Formula = "=BDP(""USDJPY Curncy"",""LAST_PRICE"")"
        .Cells(3, C_LIQ + 1).Value = "LinkedRatio"
        .Cells(4, C_LIQ + 1).Value = "RebalanceDate"
        .Cells(5, C_LIQ + 1).Value = "TotalIndexValue(=Sum Price*POST_IDX)"
        .Cells(6, C_LIQ + 1).Value = "LinkedAUM(=TotalIndexValue*0.17)"
        .Cells(3, C_LIQ + 2).Value = LINK_RATIO
        .Cells(4, C_LIQ + 2).Value = rebDate
    End With

    Application.Calculation = xlCalculationAutomatic
    Application.ScreenUpdating = True

    MsgBox "STEP1 done. BBG formulas set and event descriptions written. Run STEP2 after BBG data loaded."
End Sub

'============================
' STEP2
'============================
Public Sub STEP2_Calc_From_Sheet4()
    Dim ws As Worksheet
    Set ws = ThisWorkbook.Worksheets(SH_OUT)

    Application.ScreenUpdating = False
    Application.Calculation = xlCalculationManual

    Dim lastRow As Long
    lastRow = ws.Cells(ws.Rows.Count, C_CODE).End(xlUp).Row
    If lastRow < 2 Then GoTo ExitHere

    Dim r As Long
    For r = 2 To lastRow
        If ToDbl(ws.Cells(r, C_PX).Value2) <= 0 Then GoTo NotReady
        If ToDbl(ws.Cells(r, C_VOL20).Value2) <= 0 Then GoTo NotReady
    Next r

    CalcTradesOnSheet4 ws

ExitHere:
    Application.Calculation = xlCalculationAutomatic
    Application.ScreenUpdating = True
    MsgBox "STEP2 done."
    Exit Sub

NotReady:
    Application.Calculation = xlCalculationAutomatic
    Application.ScreenUpdating = True
    MsgBox "BBG data not ready (JPY Price / VOLUME_AVG_20D)."
End Sub

'============================
' Sheet4 layout
'============================
Private Sub PrepareSheet4Layout(ws As Worksheet)
    ws.Cells.Clear
    ws.Cells(1, C_CODE).Value = "LOCAL_CODE"
    ws.Cells(1, C_TICKER).Value = "Ticker"
    ws.Cells(1, C_NAME).Value = "Name"
    ws.Cells(1, C_PX).Value = "JPY Price"
    ws.Cells(1, C_VOL20).Value = "VOLUME_AVG_20D"
    ws.Cells(1, C_PRE_TOT).Value = "PRE_TOT_SHARES"
    ws.Cells(1, C_POST_TOT).Value = "POST_TOT_SHARES"
    ws.Cells(1, C_PRE_FFW).Value = "PRE_FFW"
    ws.Cells(1, C_POST_FFW).Value = "POST_FFW"
    ws.Cells(1, C_PRE_IDX).Value = "PRE_IDX_SHARES"
    ws.Cells(1, C_POST_IDX).Value = "POST_IDX_SHARES"
    ws.Cells(1, C_CAT).Value = "Category"
    ws.Cells(1, C_EVENTS).Value = "Events (Description)"
    ws.Cells(1, C_PRE_W).Value = "Curr Idx Wgt %"
    ws.Cells(1, C_POST_W).Value = "New Idx Wgt %"
    ws.Cells(1, C_DW).Value = "Wgt Change"
    ws.Cells(1, C_TRD_JPY).Value = "Value to Trade (JPY)"
    ws.Cells(1, C_TRD_USD).Value = "Value to Trade (USD)"
    ws.Cells(1, C_SH_TRADE).Value = "Shares to Trade"
    ws.Cells(1, C_LIQ).Value = "Liquidity"
    ' テーブルの右（T列の右）には出さない（パラメータ欄は別途出す）
End Sub

Private Sub FillSheet4UniverseAndLevels(ws As Worksheet, preD As Object, postD As Object)
    Dim allKeys As Object: Set allKeys = CreateObject("Scripting.Dictionary")
    allKeys.CompareMode = vbTextCompare

    Dim k As Variant
    For Each k In preD.Keys: allKeys(CStr(k)) = 1: Next
    For Each k In postD.Keys: allKeys(CStr(k)) = 1: Next

    Dim n As Long: n = allKeys.Count
    If n = 0 Then Exit Sub

    Dim arr() As Variant
    ReDim arr(1 To n, 1 To C_EVENTS)

    Dim i As Long: i = 0
    For Each k In allKeys.Keys
        i = i + 1
        Dim code As String: code = CStr(k)

        Dim preTot As Double: preTot = 0#
        Dim postTot As Double: postTot = 0#
        Dim preF As Double: preF = 0#
        Dim postF As Double: postF = 0#
        Dim preIdx As Double: preIdx = 0#
        Dim postIdx As Double: postIdx = 0#
        Dim cat As String: cat = ""
        Dim eventsTxt As String: eventsTxt = ""

        If preD.Exists(code) Then
            preTot = ToDbl(preD(code)("TOT_SHARES"))
            preF = ToDbl(preD(code)("IDX_FFW"))
            preIdx = ToDbl(preD(code)("IDX_SHARES"))
            If preD(code).Exists("EVENTS") Then eventsTxt = Nz(preD(code)("EVENTS"))
        End If
        If postD.Exists(code) Then
            postTot = ToDbl(postD(code)("TOT_SHARES"))
            postF = ToDbl(postD(code)("IDX_FFW"))
            postIdx = ToDbl(postD(code)("IDX_SHARES"))
            If postD(code).Exists("EVENTS") Then
                If Len(eventsTxt) = 0 Then
                    eventsTxt = Nz(postD(code)("EVENTS"))
                Else
                    eventsTxt = eventsTxt & ", " & Nz(postD(code)("EVENTS"))
                End If
            End If
        End If

        cat = GetCategory2(preTot, postTot, preF, postF, preIdx, postIdx)

        arr(i, C_CODE) = code
        arr(i, C_TICKER) = Left$(Trim$(code), 4) & BBG_SUFFIX
        arr(i, C_PRE_TOT) = preTot
        arr(i, C_POST_TOT) = postTot
        arr(i, C_PRE_FFW) = preF
        arr(i, C_POST_FFW) = postF
        arr(i, C_PRE_IDX) = preIdx
        arr(i, C_POST_IDX) = postIdx
        arr(i, C_CAT) = cat
        arr(i, C_EVENTS) = eventsTxt
    Next k

    ws.Range(ws.Cells(2, C_CODE), ws.Cells(n + 1, C_EVENTS)).Value = arr
End Sub

Private Sub SetBBGFormulas(ws As Worksheet)
    Dim lastRow As Long
    lastRow = ws.Cells(ws.Rows.Count, C_CODE).End(xlUp).Row
    If lastRow < 2 Then Exit Sub

    Dim r As Long, adrT As String
    For r = 2 To lastRow
        adrT = ws.Cells(r, C_TICKER).Address(False, False)
        ws.Cells(r, C_PX).Formula = "=BDP(" & adrT & ",""LAST_PRICE"")"
        ws.Cells(r, C_NAME).Formula = "=BDP(" & adrT & ",""NAME"")"
        ws.Cells(r, C_VOL20).Formula = "=BDP(" & adrT & ",""VOLUME_AVG_20D"")"
    Next r
End Sub

Private Sub CalcTradesOnSheet4(ws As Worksheet)
    Dim lastRow As Long
    Dim usdJpy As Double
    Dim preTotMV As Double
    Dim postTotMV As Double
    Dim r As Long
    Dim px As Double
    Dim vol20 As Double
    Dim preTot As Double
    Dim postTot As Double
    Dim preF As Double
    Dim postF As Double
    Dim preIdx As Double
    Dim postIdx As Double
    Dim preW As Double
    Dim postW As Double
    Dim dW As Double
    Dim tradeJPY As Double
    Dim tradeUSD As Double
    Dim sharesToTrade As Double
    Dim liq As Double
    Dim n As Long
    Dim outArr() As Variant
    Dim i As Long
    Dim totalIndexValue As Double
    Dim linkedAUM As Double

    lastRow = ws.Cells(ws.Rows.Count, C_CODE).End(xlUp).Row
    If lastRow < 2 Then Exit Sub

    usdJpy = ToDbl(ws.Cells(2, C_LIQ + 2).Value2)

    preTotMV = 0#: postTotMV = 0#
    For r = 2 To lastRow
        px = ToDbl(ws.Cells(r, C_PX).Value2)
        preTotMV = preTotMV + px * ToDbl(ws.Cells(r, C_PRE_IDX).Value2)
        postTotMV = postTotMV + px * ToDbl(ws.Cells(r, C_POST_IDX).Value2)
    Next r
    If preTotMV = 0 Then preTotMV = 1#
    If postTotMV = 0 Then postTotMV = 1#

    totalIndexValue = postTotMV
    linkedAUM = totalIndexValue * LINK_RATIO

    ws.Cells(5, C_LIQ + 2).Value = totalIndexValue
    ws.Cells(6, C_LIQ + 2).Value = linkedAUM

    n = lastRow - 1
    ReDim outArr(1 To n, 1 To (C_LIQ - C_PRE_W + 1))

    i = 0
    For r = 2 To lastRow
        i = i + 1
        px = ToDbl(ws.Cells(r, C_PX).Value2)
        vol20 = ToDbl(ws.Cells(r, C_VOL20).Value2)
        preTot = ToDbl(ws.Cells(r, C_PRE_TOT).Value2)
        postTot = ToDbl(ws.Cells(r, C_POST_TOT).Value2)
        preF = ToDbl(ws.Cells(r, C_PRE_FFW).Value2)
        postF = ToDbl(ws.Cells(r, C_POST_FFW).Value2)
        preIdx = ToDbl(ws.Cells(r, C_PRE_IDX).Value2)
        postIdx = ToDbl(ws.Cells(r, C_POST_IDX).Value2)

        preW = (px * preIdx) / preTotMV
        postW = (px * postIdx) / postTotMV
        dW = postW - preW

        tradeJPY = dW * linkedAUM
        If usdJpy > 0 Then tradeUSD = tradeJPY / usdJpy Else tradeUSD = 0#
        If px > 0 Then sharesToTrade = tradeJPY / px Else sharesToTrade = 0#
        If vol20 > 0 Then liq = sharesToTrade / vol20 Else liq = 0#

        outArr(i, 1) = preW
        outArr(i, 2) = postW
        outArr(i, 3) = dW
        outArr(i, 4) = tradeJPY
        outArr(i, 5) = tradeUSD
        outArr(i, 6) = sharesToTrade
        outArr(i, 7) = liq
    Next r

    ws.Range(ws.Cells(2, C_PRE_W), ws.Cells(lastRow, C_LIQ)).Value = outArr

    ws.Columns(C_PX).NumberFormat = "#,##0.00"
    ws.Range(ws.Columns(C_PRE_W), ws.Columns(C_DW)).NumberFormat = "0.0000%"
    ws.Range(ws.Columns(C_TRD_JPY), ws.Columns(C_SH_TRADE)).NumberFormat = "#,##0"
    ws.Columns(C_LIQ).NumberFormat = "0.00%"
    ws.Cells(5, C_LIQ + 2).NumberFormat = "#,##0"
    ws.Cells(6, C_LIQ + 2).NumberFormat = "#,##0"
    ws.Cells(7, C_LIQ + 2).NumberFormat = "#,##0.00"
End Sub

'=== 補助関数 ===

Private Function GetCategory2(preTot As Double, postTot As Double, preF As Double, postF As Double, preIdx As Double, postIdx As Double) As String
    If preIdx = 0 And postIdx > 0 Then GetCategory2 = "Addition": Exit Function
    If preIdx > 0 And postIdx = 0 Then GetCategory2 = "Deletion": Exit Function
    If (postF - preF) > 0.000001 Then GetCategory2 = "FFW Increase": Exit Function
    If (postF - preF) < -0.000001 Then GetCategory2 = "FFW Decrease": Exit Function
    If (postTot - preTot) > 1 Then GetCategory2 = "Sh. Increase": Exit Function
    If (postTot - preTot) < -1 Then GetCategory2 = "Sh. Decrease": Exit Function
    If preIdx = 0 And postIdx = 0 Then GetCategory2 = "NULL" Else GetCategory2 = "Funding/Reinvestment"
End Function

Private Function LoadPortfolio_S2(ws As Worksheet) As Object
    Dim dict As Object: Set dict = CreateObject("Scripting.Dictionary")
    dict.CompareMode = vbTextCompare

    Dim lastRow As Long, lastCol As Long
    lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
    lastCol = ws.Cells(1, ws.Columns.Count).End(xlToLeft).Column

    Dim data As Variant
    data = ws.Range(ws.Cells(1, 1), ws.Cells(lastRow, lastCol)).Value2

    Dim cCode As Long: cCode = FindCol(data, "LOCAL_CODE")
    Dim cName As Long: cName = FindCol(data, "LOCAL_NAME")
    Dim cPrc  As Long: cPrc = FindCol(data, "PRC_BASE")
    Dim cTot  As Long: cTot = FindCol(data, "TOT_SHARES")
    Dim cFFW  As Long: cFFW = FindCol(data, "IDX_FFW")
    Dim cIdxS As Long: cIdxS = FindCol(data, "IDX_SHARES")

    Dim r As Long
    For r = 2 To UBound(data, 1)
        If Len(data(r, cCode) & vbNullString) = 0 Then GoTo ContinueR

        Dim code As String: code = CStr(data(r, cCode))

        Dim h As Object: Set h = CreateObject("Scripting.Dictionary")
        h.CompareMode = vbTextCompare
        h("CODE") = code
        h("NAME") = Nz(data(r, cName))
        h("PRC") = ToDbl(data(r, cPrc))
        h("TOT_SHARES") = ToDbl(data(r, cTot))
        h("IDX_FFW") = ToDbl(data(r, cFFW))
        h("IDX_SHARES") = ToDbl(data(r, cIdxS))
        h("EVENTS") = ""

        If dict.Exists(code) Then
            Set dict(code) = h
        Else
            dict.Add code, h
        End If

ContinueR:
    Next r

    Set LoadPortfolio_S2 = dict
End Function

Private Function GetBaseDate(ws As Worksheet) As Long
    Dim lastCol As Long: lastCol = ws.Cells(1, ws.Columns.Count).End(xlToLeft).Column
    Dim data As Variant
    data = ws.Range(ws.Cells(1, 1), ws.Cells(2, lastCol)).Value2
    GetBaseDate = CLng(ToDbl(data(2, FindCol(data, "TARGET_DATE"))))
End Function

Private Sub ApplyChanges_S3_Overwrite(ByRef holdings As Object, ws As Worksheet, fromDate As Long, toDate As Long)
    If toDate < fromDate Then Exit Sub

    Dim lastRow As Long, lastCol As Long
    lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
    lastCol = ws.Cells(1, ws.Columns.Count).End(xlToLeft).Column

    Dim data As Variant
    data = ws.Range(ws.Cells(1, 1), ws.Cells(lastRow, lastCol)).Value2

    Dim cCode As Long: cCode = FindCol(data, "LOCAL_CODE")
    Dim cEff  As Long: cEff = FindCol(data, "EFFECTIVE_DATE")
    Dim cEvt  As Long: cEvt = FindColAllowMissing(data, "EVENT_CODE")

    Dim cName As Long: cName = FindColAllowMissing(data, "LOCAL_NAME")
    Dim cIdxS As Long: cIdxS = FindColAllowMissing(data, "IDX_SHARES")
    Dim cTot  As Long: cTot = FindColAllowMissing(data, "TOT_SHARES")
    Dim cNewFFW As Long: cNewFFW = FindColAllowMissing(data, "NEW_IDX_FFW")

    Dim r As Long
    For r = 2 To UBound(data, 1)
        If Len(data(r, cCode) & vbNullString) = 0 Then GoTo ContinueR

        Dim eff As Long: eff = CLng(ToDbl(data(r, cEff)))
        If eff < fromDate Or eff > toDate Then GoTo ContinueR

        Dim code As String: code = CStr(data(r, cCode))

        Dim h As Object
        If holdings.Exists(code) Then
            Set h = holdings(code)
        Else
            Set h = CreateObject("Scripting.Dictionary")
            h.CompareMode = vbTextCompare
            h("CODE") = code
            h("NAME") = IIf(cName > 0, Nz(data(r, cName)), "")
            h("PRC") = 0#
            h("TOT_SHARES") = 0#
            h("IDX_FFW") = 0#
            h("IDX_SHARES") = 0#
            h("EVENTS") = ""
            holdings.Add code, h
        End If

        ' Append event description if present
        If cEvt > 0 Then
            Dim evtVal As String
            evtVal = Nz(data(r, cEvt))
            If Len(evtVal) > 0 Then
                Dim desc As String
                desc = GetEventDescription(evtVal)
                If Len(Nz(h("EVENTS"))) = 0 Then
                    h("EVENTS") = desc
                Else
                    h("EVENTS") = h("EVENTS") & ", " & desc
                End If
            End If
        End If

        ' Ignore non-impact events
        If cEvt > 0 Then
            Dim evtCode As String: evtCode = Nz(data(r, cEvt))
            If evtCode = "30" Or evtCode = "C1" Or evtCode = "SK" Then GoTo ContinueR
        End If

        ' Deletion-like
        If cEvt > 0 Then
            Dim evt2 As String: evt2 = Nz(data(r, cEvt))
            If evt2 = "19" Or evt2 = "C2" Or evt2 = "P1" Then
                h("TOT_SHARES") = 0#
                h("IDX_SHARES") = 0#
                GoTo ContinueR
            End If
        End If

        If cTot > 0 Then If HasValue(data(r, cTot)) Then h("TOT_SHARES") = ToDbl(data(r, cTot))
        If cIdxS > 0 Then If HasValue(data(r, cIdxS)) Then h("IDX_SHARES") = ToDbl(data(r, cIdxS))
        If cNewFFW > 0 Then If HasValue(data(r, cNewFFW)) Then h("IDX_FFW") = ToDbl(data(r, cNewFFW))

ContinueR:
    Next r
End Sub

Private Function CloneHoldings(src As Object) As Object
    Dim dst As Object: Set dst = CreateObject("Scripting.Dictionary")
    dst.CompareMode = vbTextCompare
    Dim k As Variant
    For Each k In src.Keys
        Dim h0 As Object: Set h0 = src(k)
        Dim h As Object: Set h = CreateObject("Scripting.Dictionary")
        h.CompareMode = vbTextCompare
        Dim kk As Variant
        For Each kk In h0.Keys
            h(kk) = h0(kk)
        Next kk
        dst.Add CStr(k), h
    Next k
    Set CloneHoldings = dst
End Function

Private Function FindCol(data As Variant, headerName As String) As Long
    Dim j As Long
    For j = 1 To UBound(data, 2)
        If UCase$(Trim$(CStr(data(1, j)))) = UCase$(headerName) Then
            FindCol = j: Exit Function
        End If
    Next j
    Err.Raise vbObjectError + 100, , "Missing column: " & headerName
End Function

Private Function FindColAllowMissing(data As Variant, headerName As String) As Long
    Dim j As Long
    For j = 1 To UBound(data, 2)
        If UCase$(Trim$(CStr(data(1, j)))) = UCase$(headerName) Then
            FindColAllowMissing = j: Exit Function
        End If
    Next j
    FindColAllowMissing = 0
End Function

Private Function Nz(v As Variant) As String
    If IsEmpty(v) Or IsNull(v) Or IsError(v) Then Nz = "" Else Nz = CStr(v)
End Function

Private Function HasValue(v As Variant) As Boolean
    If IsError(v) Then HasValue = False: Exit Function
    If IsEmpty(v) Or IsNull(v) Then HasValue = False: Exit Function
    Dim s As String: s = Trim$(CStr(v))
    If s = "" Or UCase$(s) = "NULL" Then HasValue = False Else HasValue = True
End Function

Private Function ToDbl(v As Variant) As Double
    On Error GoTo EH
    If IsError(v) Or IsEmpty(v) Or IsNull(v) Then ToDbl = 0#: Exit Function
    Dim s As String: s = Trim$(CStr(v))
    If s = "" Or UCase$(s) = "NULL" Then ToDbl = 0#: Exit Function
    s = Replace(s, ",", "")
    ToDbl = CDbl(s)
    Exit Function
EH:
    ToDbl = 0#
End Function

Private Function GetEventDescription(evt As String) As String
    Select Case UCase$(Trim$(evt))
        Case "0": GetEventDescription = "New Listing (IPO)"
        Case "2": GetEventDescription = "Public Offering"
        Case "5": GetEventDescription = "Rights Issue"
        Case "6": GetEventDescription = "Third-Party Allotment"
        Case "8": GetEventDescription = "Conversion of Convertible Bonds"
        Case "13": GetEventDescription = "Capital Reduction"
        Case "14": GetEventDescription = "Merger"
        Case "15": GetEventDescription = "Reverse Share Split"
        Case "16": GetEventDescription = "Exercise of Warrants"
        Case "18": GetEventDescription = "Conversion of Preferred Shares"
        Case "19": GetEventDescription = "Delisting"
        Case "30": GetEventDescription = "Change in Trading Unit"
        Case "90": GetEventDescription = "Other Adjustments"
        Case "95": GetEventDescription = "Exercise of Stock Options"
        Case "96": GetEventDescription = "Corporate Split"
        Case "B1": GetEventDescription = "Stock Split"
        Case "C1": GetEventDescription = "Change of Industry Classification"
        Case "C2": GetEventDescription = "Transfer Between Market Sections"
        Case "FR": GetEventDescription = "Change in FFW"
        Case "J1": GetEventDescription = "Retirement of Treasury Stock"
        Case "P1": GetEventDescription = "Securities Marked for Delisting"
        Case "SK": GetEventDescription = "Reclassification of Size Category in TOPIX"
        Case Else: GetEventDescription = evt
    End Select
End Function

